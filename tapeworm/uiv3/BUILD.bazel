load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin", "nodejs_binary")
load("@npm//next:index.bzl", "next")

filegroup(
    name = "source_files",
    srcs = glob([
        "pages/*",
        "styles/*",
    ]) + [
        "tsconfig.json",
        "next.config.js",
        "next-env.d.ts",
        "tailwind.config.js",
        "postcss.config.js",
    ],
)

copy_to_bin(
    name = "copy_source_files",
    srcs = ["source_files"],
)

NPM_DEPENDENCIES = [
    "@npm//@types/node",
    "@npm//@types/react",
    "@npm//@types/lunr",
    "@npm//react",
    "@npm//react-dom",
    "@npm//typescript",
    "@npm//tailwindcss",
    "@npm//postcss",
    "@npm//cssnano",
    "@npm//autoprefixer",
    "@npm//react-query",
    "@npm//react-toast-notifications",
    "@npm//@headlessui/react",
    "@npm//@heroicons/react",
    "@npm//lunr",
    "@npm//awesome-debounce-promise",
    "@npm//use-constant",
    "@npm//react-async-hook",
    "@npm//date-fns",
]

next(
    name = "build",
    outs = [
        ".next",
    ],
    args = [
        "build",
        "$(RULEDIR)",
    ],
    data = ["copy_source_files"] + NPM_DEPENDENCIES,
    env = {
        "NODE_ENV": "production",
    },
)

next(
    name = "dev",
    args = [
        "dev",

        # Root of app is the directory in which this BUILD.bazel file is located in
        package_name(),

        # Set port of development server to 3001
        "-p 3000",

        # To prevent webpack from bundling multiple react versions
        "--node_options=--preserve-symlinks-main",
        "--bazel_run_from_execroot",
    ],
    data = ["copy_source_files"] + NPM_DEPENDENCIES,
    env = {
        "NODE_ENV": "development",
    },
    tags = ["ibazel_notify_changes"],
)

next(
    name = "export",
    outs = [
        "out",
    ],
    args = [
        "export",
        "$(RULEDIR)",
    ],
    data = [
        ":build",
    ] + NPM_DEPENDENCIES,
    env = {
        "NODE_ENV": "production",
    },
)

load("@npm//http-server:index.bzl", "http_server")

http_server(
    name = "server",
    args = ["tapeworm/uiv3/out"],
    data = [
        "export",
    ],
)

load("@rules_pkg//:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "tar",
    srcs = [
        ":export",
    ],
    mode = "0755",
    package_dir = "/app/static",
    strip_prefix = "./out",
    visibility = ["//visibility:public"],
)
